plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
    id "edu.sc.seis.launch4j" version "2.5.1"
    id "io.freefair.lombok" version "6.4.0"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.fabricmc.net/" }
}

dependencies {
    // Some misc annotations to help with context hints and autocompletion.
    // https://www.jetbrains.com/help/idea/annotating-source-code.html
    implementation 'org.jetbrains:annotations:23.0.0'

    // Used to handle JSON data objects.
    // https://github.com/falkreon/Jankson
    implementation 'blue.endless:jankson:1.2.1'

    // Used to validate various URIs.
    // https://mvnrepository.com/artifact/commons-validator/commons-validator
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

    // Used to verify Minecraft and program arguments.
    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.5.0'

    // Used for parsing markdown various markdown styles. Has custom markdown support too.
    // https://github.com/commonmark/commonmark-java
    implementation "org.commonmark:commonmark:0.18.1"
    implementation "org.commonmark:commonmark-ext-autolink:0.18.1"

    // Used to launch Minecraft. Also enables fabric functionality.
    // https://fabricmc.net
    implementation "com.github.calmilamsy:cursed-fabric-loader:91f8c4c"

    // Used to handle auth
    // https://github.com/chris54721/OpenMCAuthenticator
    implementation 'com.github.chris54721:OpenMCAuthenticator:1.3.1'

    // Used to handle accessing the mod repo.
    // https://glass-repo.net
    // No public documentation yet, though if you figure it out, you can use it for whatever.
    implementation group: "com.github.calmilamsy", name: "Glass-Site-Repo-Java-API", version: "f623e78"

    // Various utility classes that help dramatically with handling files and URLs.
    // https://glass-launcher.net
    // Same thing, use if you want, though there are no docs.
    implementation group: "com.github.calmilamsy", name: "Glass-Commons", version: "4aa5302"

    implementation "com.github.calmilamsy:glass-launch-wrapper:a729713"

    annotationProcessor implementation ("org.ow2.asm:asm:9.0")
    annotationProcessor implementation ("org.ow2.asm:asm-analysis:9.0")
    annotationProcessor implementation ("org.ow2.asm:asm-commons:9.0")
    annotationProcessor implementation ("org.ow2.asm:asm-tree:9.0")
    annotationProcessor implementation ("org.ow2.asm:asm-util:9.0")

    implementation('net.fabricmc:sponge-mixin:0.11.1+mixin.0.8.5') {
        exclude module: 'launchwrapper'
    }
    implementation 'net.fabricmc:tiny-mappings-parser:0.2.2.14'
    implementation 'net.fabricmc:tiny-remapper:0.3.0.70'
    implementation 'net.fabricmc:access-widener:1.0.0'

    implementation 'com.google.jimfs:jimfs:1.2-fabric'
    implementation 'net.fabricmc:fabric-loader-sat4j:2.3.5.4'

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'
}

group 'glass-launcher'
version '0.5.0'

// Tells gradle to scream at you if you try to use any post Java 8 features.
sourceCompatibility = 1.8

// Ensures encoding doesn't get screwed up when compiling on various OSes and language encodings.
// If you get an error when compiling, remove any special characters you added.
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Debug task for when repos break.
task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
    }
}

// Creates a fully functional exe on windows. Does not work on other OSes (duh).
createExe {
    if (org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem.isWindows()) {
        mainClassName = 'net.glasslauncher.legacy.Main'
        icon = "${projectDir}/glass.ico"
        launch4j.outputDir = "libs"
        launch4j.outfile = "${project.name}-${project.version}.exe"
        jar = project.tasks["shadowJar"].archiveName
        doLast {
            // https://github.com/TheBoegl/gradle-launch4j/issues/68
            delete("${buildDir}/${launch4j.outputDir}/${launch4j.libraryDir}")
        }
    }
}

tasks.build.dependsOn(tasks.shadowJar)
tasks.build.dependsOn(tasks.createExe)

jar.manifest.attributes 'Implementation-Title': 'Glass Launcher Legacy',
        'Implementation-Version': version,
        'Main-Class': 'net.glasslauncher.legacy.Main'
