import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java'
    id "edu.sc.seis.launch4j" version "2.4.9"
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url  "https://maven.fabricmc.net/" }
}

dependencies {
    // Used to make handling data objects far easier.
    // https://projectlombok.org/
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    // Some misc annotations to help with context hints and autocompletion.
    // https://www.jetbrains.com/help/idea/annotating-source-code.html
    compile 'org.jetbrains:annotations:16.0.2'

    // Used to run the proxy included in the launcher.
    // https://github.com/shs96c/LittleProxy
    compile group: 'com.github.shs96c', name: 'littleproxy', version: '1fcc11f'

    // Used to hijack and replace urls in the proxy included in glass-launcher.
    // https://repo1.maven.org/maven2/com/github/ganskef/littleproxy-mitm
    compile (group: 'com.github.ganskef', name: 'littleproxy-mitm', version: '1.1.0' ) {
        exclude group: "org.littleshoot", module: "littleproxy"
    }

    // Used to handle JSON data objects.
    // https://repo1.maven.org/maven2/com/google/code/gson/gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Used to validate various URIs.
    // https://mvnrepository.com/artifact/commons-validator/commons-validator
    compile group: 'commons-validator', name: 'commons-validator', version: '1.6'

    // Used to verify Minecraft and program arguments.
    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    // Used for parsing markdown various markdown styles. Has custom markdown support too.
    // https://github.com/commonmark/commonmark-java
    compile "org.commonmark:commonmark:0.17.0"
    compile "org.commonmark:commonmark-ext-autolink:0.17.0"

    // Used to launch Minecraft. Also enables fabric functionality.
    // https://fabricmc.net
    compile "com.github.minecraft-cursed-legacy:cursed-fabric-loader:671a1bb"

    // Fabric Loader dependencies start
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'
    implementation 'com.google.guava:guava:28.0-jre'
    implementation 'org.ow2.asm:asm:9.0'
    implementation 'org.ow2.asm:asm-analysis:9.0'
    implementation 'org.ow2.asm:asm-commons:9.0'
    implementation 'org.ow2.asm:asm-tree:9.0'
    implementation 'org.ow2.asm:asm-util:9.0'
    implementation 'net.fabricmc:access-widener:1.0.0'
    compile "net.fabricmc:sponge-mixin:0.8+build.18"
    compile "net.fabricmc:tiny-mappings-parser:0.2.2.14"
    compile "net.fabricmc:tiny-remapper:0.3.0.70"
    compile "com.google.jimfs:jimfs:1.1"
    compile "net.fabricmc:fabric-loader-sat4j:2.3.5.4"
    compile "com.google.guava:guava:21.0"
    compile "commons-io:commons-io:2.5"
    compile "org.apache.commons:commons-lang3:3.5"
    compile "net.sf.jopt-simple:jopt-simple:5.0.4"
    compile "tk.valoeghese:ZoesteriaConfig:1.3.2"
    // Fabric Loader dependencies end

    // Used to handle auth
    // https://github.com/chris54721/OpenMCAuthenticator
    implementation 'com.github.chris54721:OpenMCAuthenticator:1.3.1'

    // Used to handle accessing the mod repo.
    // https://glass-repo.net
    // No public documentation yet, though if you figure it out, you can use it for whatever.
    compile group: "com.github.calmilamsy", name: "Glass-Site-Repo-Java-API", version: "4aa5302"

    // Various utility classes that help dramatically with handling files and URLs.
    // https://glass-launcher.net
    // Same thing, use if you want, though there are no docs.
    compile group: "com.github.calmilamsy", name: "Glass-Commons", version: "e8cacac"

}

group 'glass-launcher'
version '0.5.0'

// Tells gradle to scream at you if you try to use any post Java 8 features.
sourceCompatibility = 1.8

// Ensures encoding doesn't get screwed up when compiling on various OSes and language encodings.
// If you get an error when compiling, remove any special characters you added.
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Debug task for when repos break.
task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
    }
}

// Creates a fully functional exe on windows. Does not work on other OSes (duh).
createExe {
    if (DefaultNativePlatform.currentOperatingSystem.isWindows()) {
        mainClassName = 'net.glasslauncher.legacy.Main'
        icon = "${projectDir}/glass.ico"
        launch4j.outputDir = "libs"
        launch4j.outfile = "${project.name}-${project.version}.exe"
        jar = project.tasks["shadowJar"].archiveName
        doLast {
            // https://github.com/TheBoegl/gradle-launch4j/issues/68
            delete("${buildDir}/${launch4j.outputDir}/${launch4j.libraryDir}")
        }
    }
}

tasks.build.dependsOn(tasks.shadowJar)
tasks.build.dependsOn(tasks.createExe)

jar {
    manifest {
        attributes 'Implementation-Title': 'Glass Launcher Legacy',
                'Implementation-Version': version,
                'Main-Class': 'net.glasslauncher.legacy.Main'
    }
}
